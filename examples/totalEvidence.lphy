data {
   numLoci = 8;
   L = rep(element=500, times=numLoci);
 }
 model {
   lambda ~ Uniform(lower=1.0, upper=1.5);
   mu ~ Uniform(lower=0.5, upper=1.0);
   origin ~ Uniform(lower=2, upper=2.5);
   psi = 1.0;
   S ~ SimFBDAge(mu=mu, lambda=lambda, psi=psi, frac=1.0, originAge=origin);
   morph_mu ~ LogNormal(meanlog=-3.0, sdlog=0.1);
   clock_rate ~ LogNormal(meanlog=-3, sdlog=0.1);
   morph_data ~ PhyloCTMC(tree=S, Q=lewisMK(numStates=2), mu=morph_mu, L=100);
   alpha = 2.0;
   beta = 0.5;
   pop_sizes ~ InverseGamma(alpha=alpha, beta=beta, n=S.nodeCount());

   n = rep(element=1, times=S.extantCount());

   g ~ MultispeciesCoalescent(S=S, theta=pop_sizes, numLoci=numLoci, n=n);
   g_rate ~ WeightedDirichlet(conc=rep(element=1.0, times=numLoci), weights=L);
   kappa ~ LogNormal(meanlog=1.0, sdlog=1.25, n=numLoci);
   pi ~ Dirichlet(conc=[2,2,2,2], n=numLoci);
   mol_data ~ PhyloCTMC(Q=hky(kappa=kappa, freq=pi, meanRate=g_rate), mu=clock_rate, tree=g, L=L);
 }