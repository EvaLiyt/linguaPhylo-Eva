data {
  spTaxa = taxa(names=1:20);
  gTaxa = taxa(names=["1_1","2_1","3_1","4_1","5_1","6_1","7_1","8_1","9_1","10_1","11_1","12_1","13_1","14_1","15_1","16_1","17_1","18_1","19_1","20_1"], species=1:20);
}
model {
  lambda ~ Uniform(lower=1.0, upper=1.5);
  mu ~ Uniform(lower=0.5, upper=1.0);
  psi = 1.0;
  S ~ FossilBirthDeathTree(mu=mu, lambda=lambda, psi=psi, rho=1.0, taxa=spTaxa);
  morph_mu ~ LogNormal(meanlog=-3.0, sdlog=0.1);
  clock_rate ~ LogNormal(meanlog=-3, sdlog=0.1);
  morph_data ~ PhyloCTMC(tree=S, Q=lewisMK(numStates=2), mu=morph_mu, L=100);
  alpha = 2.0;
  beta ~ LogNormal(meanlog=-1.0, sdlog=1.25);
  pop_sizes ~ InverseGamma(alpha=alpha, beta=beta, n=S.nodeCount());

  g ~ MultispeciesCoalescent(S=S, theta=pop_sizes, taxa=gTaxa, numLoci=3);
  kappa ~ LogNormal(meanlog=1.0, sdlog=1.25, n=3);
  g_rate ~ LogNormal(meanlog=0.0, sdlog=1.0, n=3);

  pi1 ~ Dirichlet(conc=[2,2,2,2]);
  mol_data1 ~ PhyloCTMC(Q=hky(kappa=kappa[0], freq=pi1, rate=g_rate[0]), mu=clock_rate, tree=g[0], L=500);

  pi2 ~ Dirichlet(conc=[2,2,2,2]);
  mol_data2 ~ PhyloCTMC(Q=hky(kappa=kappa[1], freq=pi2, rate=g_rate[1]), mu=clock_rate, tree=g[1], L=500);

  pi3 ~ Dirichlet(conc=[2,2,2,2]);
  mol_data3 ~ PhyloCTMC(Q=hky(kappa=kappa[2], freq=pi3, rate=g_rate[2]), mu=clock_rate, tree=g[2], L=500);
}