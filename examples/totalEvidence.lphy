data {
  spTaxa = taxa(names=1:20);
  gTaxa = taxa(names=["1_1","2_1","3_1","4_1","5_1","6_1","7_1","8_1","9_1","10_1","11_1","12_1","13_1","14_1","15_1","16_1","17_1","18_1","19_1","20_1"], species=1:20);
  numLoci = 8;
  L = rep(element=500, times=numLoci);
}
model {
  lambda ~ Uniform(lower=1.0, upper=1.5);
  mu ~ Uniform(lower=0.5, upper=1.0);
  psi = 1.0;
  S ~ FossilBirthDeathTree(mu=mu, lambda=lambda, psi=psi, rho=1.0, taxa=spTaxa);
  morph_mu ~ LogNormal(meanlog=-3.0, sdlog=0.1);
  clock_rate ~ LogNormal(meanlog=-3, sdlog=0.1);
  morph_data ~ PhyloCTMC(tree=S, Q=lewisMK(numStates=2), mu=morph_mu, L=100);
  alpha = 2.0;
  beta ~ LogNormal(meanlog=-1.0, sdlog=1.25);
  pop_sizes ~ InverseGamma(alpha=alpha, beta=beta, n=S.nodeCount());

  g ~ MultispeciesCoalescent(S=S, theta=pop_sizes, taxa=gTaxa, numLoci=numLoci);
  g_rate ~ WeightedDirichlet(conc=rep(element=1.0, times=numLoci), weights=L);
  kappa ~ LogNormal(meanlog=1.0, sdlog=1.25, n=numLoci);
  pi ~ Dirichlet(conc=[2,2,2,2], n=numLoci);
  mol_data ~ PhyloCTMC(Q=hky(kappa=kappa, freq=pi, meanRate=g_rate), mu=clock_rate, tree=g, L=L);
}